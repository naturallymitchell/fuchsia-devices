# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("aml-scpi-s912-bind") {
  rules = "aml-scpi-s912.bind"
  header_output = "aml-scpi-s912-bind.h"
  tests = "aml-scpi-s912-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

driver_bind_rules("aml-mailbox-s912-bind") {
  rules = "aml-mailbox-s912.bind"
  header_output = "aml-mailbox-s912-bind.h"
  tests = "aml-mailbox-s912-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

group("aml-scpi-s912") {
  deps = [
    ":aml-scpi-s912.mailbox",
    ":scpi",
  ]
}

fuchsia_driver("aml-scpi-s912.mailbox-driver") {
  output_name = "aml-scpi-s912.mailbox"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "aml-mailbox.cc" ]
  deps = [
    ":aml-mailbox-s912-bind",
    "//sdk/banjo/fuchsia.hardware.clock:fuchsia.hardware.clock_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("aml-scpi-s912.mailbox") {
  component_name = "aml-scpi-s912.mailbox"
  deps = [ ":aml-scpi-s912.mailbox-driver" ]
}

fuchsia_driver("scpi-driver") {
  output_name = "scpi"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "aml-scpi.cc" ]
  deps = [
    ":aml-scpi-s912-bind",
    "//sdk/banjo/fuchsia.hardware.mailbox:fuchsia.hardware.mailbox_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.scpi:fuchsia.hardware.scpi_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("scpi") {
  component_name = "scpi"
  deps = [ ":scpi-driver" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-mailbox-s912-bind_test",
    ":aml-scpi-s912-bind_test",
  ]
}
