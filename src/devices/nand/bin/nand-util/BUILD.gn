# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

executable("nand-util") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "aml.cc",
    "commands.cc",
    "ftl.cc",
    "main.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/cksum",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-hardware-nand:c",
  ]
}

source_set("common") {
  visibility = [ ":*" ]
  sources = [
    "ftl_internal.cc",
    "nand-broker.cc",
  ]
  deps = [ "//zircon/public/lib/pretty" ]
  public_deps = [
    "//sdk/fidl/fuchsia.nand:fuchsia.nand_c",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fzl",
    "//zircon/system/fidl/fuchsia-device:c",
    "//zircon/system/fidl/fuchsia-device:llcpp",
    "//zircon/system/fidl/fuchsia-hardware-nand:c",
  ]

  # TODO(41887): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  configs += [ "//build/config:temporarily_disable_ubsan_do_not_use" ]
}

test("nand-util-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  if (is_fuchsia) {
    testonly = false
  }
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "nand-util-test"
  sources = [ "test/ftl_internal_test.cc" ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zxtest",
  ]
}

migrated_manifest("nand-util-manifest") {
  deps = [ ":nand-util" ]
}

migrated_manifest("nand-util-test-manifest") {
  deps = [ ":nand-util-test" ]
}
