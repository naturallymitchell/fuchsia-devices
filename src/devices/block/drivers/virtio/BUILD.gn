# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("virtio_block") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "block_driver.cc" ]
  deps = [
    ":block_lib",
    "//sdk/banjo/ddk.protocol.block",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
  ]
}

source_set("block_lib") {
  configs += [ "//build/config:all_source" ]
  sources = [ "block.cc" ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.block",
    "//src/devices/bus/lib/virtio",
    "//src/lib/ddktl",
    "//src/lib/listnode",
  ]
}

source_set("scsi_lib") {
  configs += [ "//build/config:all_source" ]
  sources = [ "scsi.cc" ]
  public_deps = [
    "//sdk/banjo/ddk.protocol.block",
    "//src/devices/block/lib/scsi",
    "//src/devices/bus/lib/virtio",
    "//src/lib/ddktl",
  ]
}

driver_module("virtio_scsi") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "scsi_driver.cc" ]
  deps = [
    ":scsi_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
  ]
}

test("virtio-block-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "block_test.cc" ]
  deps = [
    ":block_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
}

test("virtio-scsi-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "scsi_test.cc" ]
  deps = [
    ":scsi_lib",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":virtio-block-test-package",
    ":virtio-scsi-test-package",
  ]
}

unittest_package("virtio-block-test-package") {
  package_name = "virtio-block-test"
  deps = [ ":virtio-block-test" ]

  tests = [
    {
      name = "virtio-block-test"
    },
  ]
}

unittest_package("virtio-scsi-test-package") {
  package_name = "virtio-scsi-test"
  deps = [ ":virtio-scsi-test" ]

  tests = [
    {
      name = "virtio-scsi-test"
    },
  ]
}
