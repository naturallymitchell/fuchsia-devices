# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")

group("tests") {
  testonly = true
  deps = [
    ":ftl_bind_test",
    "tests",
  ]
}

bind_rules("ftl_bind") {
  rules = "ftl.bind"
  output = "ftl_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.nand" ]
}

driver_module("ftl") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "bind.cc" ]
  deps = [
    ":common",
    ":ftl_bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
  ]
}

# Also used by ftl-test.
source_set("common") {
  visibility = [ "./*" ]
  configs += [ "//build/config:all_source" ]
  sources = [
    "block_device.cc",
    "metrics.cc",
    "nand_driver.cc",
    "nand_operation.cc",
    "oob_doubler.cc",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.badblock",
    "//sdk/banjo/fuchsia.hardware.block",
    "//sdk/banjo/fuchsia.hardware.block.partition",
    "//sdk/banjo/fuchsia.hardware.nand",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ftl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/trace:trace-driver",
    "//zircon/system/ulib/zxc",
  ]
}
