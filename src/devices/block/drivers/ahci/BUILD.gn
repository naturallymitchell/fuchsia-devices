# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("ahci") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  deps = [
    ":shared",
    "//src/devices/lib/driver",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ahci-unittest-package" ]
}

test("ahci-unittest") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "ahci-unittest"
  configs += [ "//build/config:all_source" ]
  sources = [
    "test/fake-bus.cc",
    "test/main.cc",
  ]
  deps = [
    ":shared",
    "//sdk/banjo/ddk.protocol.block",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("ahci-unittest-package") {
  package_name = "ahci-unittest"
  deps = [ ":ahci-unittest" ]
  tests = [
    {
      name = "ahci-unittest"
    },
  ]
}

source_set("shared") {
  visibility = [ "./*" ]
  configs += [ "//build/config:all_source" ]
  sources = [
    "bus.cc",
    "controller.cc",
    "pci-bus.cc",
    "port.cc",
    "sata.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.block",
    "//sdk/banjo/ddk.protocol.pci",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
  ]
}
