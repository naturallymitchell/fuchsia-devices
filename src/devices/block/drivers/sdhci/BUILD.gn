# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("sdhci-bind") {
  rules = "sdhci.bind"
  header_output = "sdhci-bind.h"
  tests = "sdhci-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.sdmmc" ]
}

fuchsia_driver("sdhci-driver") {
  output_name = "sdhci"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "sdhci.cc" ]
  deps = [
    ":sdhci-bind",
    "//sdk/banjo/fuchsia.hardware.block:fuchsia.hardware.block_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdhci:fuchsia.hardware.sdhci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("sdhci") {
  component_name = "sdhci"
  deps = [ ":sdhci-driver" ]
}

test("sdhci-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "sdhci-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "sdhci-test.cc",
    "sdhci.cc",
  ]
  deps = [
    ":sdhci-bind",
    "//sdk/banjo/fuchsia.hardware.block:fuchsia.hardware.block_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdhci:fuchsia.hardware.sdhci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdhci:fuchsia.hardware.sdhci_banjo_cpp_mock",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("sdhci-test") {
  deps = [ ":sdhci-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":sdhci-bind_test",
    ":sdhci-test",
  ]
}
