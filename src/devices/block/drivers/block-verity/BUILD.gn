# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

source_set("geometry") {
  sources = [ "geometry.cc" ]
}

test("geometry-test") {
  testonly = true

  output_name = "geometry-test"
  sources = [ "geometry-tests.cc" ]
  deps = [
    ":geometry",
    "//zircon/public/lib/zxtest",
  ]
}

source_set("sealer") {
  sources = [
    "hash-block-accumulator.cc",
    "sealer.cc",
  ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/digest",
  ]
}

test("sealer-test") {
  testonly = true

  output_name = "sealer-test"
  sources = [ "sealer-tests.cc" ]
  deps = [
    ":geometry",
    ":sealer",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/digest",
  ]
}

driver_module("block-verity") {
  output_name = "block-verity"

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  sources = [
    "device-info.cc",
    "device-manager.cc",
    "device.cc",
    "driver-sealer.cc",
    "extra.cc",
  ]

  configs += [ "//build/config/fuchsia:integer-paranoia" ]

  deps = [
    ":geometry",
    ":sealer",
    "//sdk/banjo/ddk.protocol.block",
    "//sdk/fidl/fuchsia.hardware.block.verified:fuchsia.hardware.block.verified_llcpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/digest",
  ]
}

test("block-verity-test") {
  output_name = "block-verity-test"
  sources = [ "block-verity-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.hardware.block.verified:fuchsia.hardware.block.verified_llcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/driver-integration-test",
    "//zircon/system/ulib/fvm-devices",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

fuchsia_unittest_package("block-verity-geometry-test") {
  executable_path = "test/geometry-test"
  deps = [ ":geometry-test" ]
}

fuchsia_unittest_package("block-verity-sealer-test") {
  executable_path = "test/sealer-test"
  deps = [ ":sealer-test" ]
}

isolated_devmgr_unittest_package("block-verity-integration-test-package") {
  package_name = "block-verity-test"
  executable_path = "test/block-verity-test"
  deps = [
    ":block-verity",
    ":block-verity-test",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":block-verity-geometry-test",
    ":block-verity-integration-test-package",
    ":block-verity-sealer-test",
  ]
}
