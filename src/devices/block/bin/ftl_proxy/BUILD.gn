# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//src/sys/build/components.gni")
import("//third_party/cobalt/metrics_registry.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

group("tests") {
  testonly = true
  deps = [ ":ftl-proxy-tests" ]
}

metrics_registry("local_storage_metrics") {
  project_id = 3676913920
  namespace = "cobalt_registry"
  generate_cc = true
  generate_binarypb = false
}

source_set("ftl-util") {
  sources = [ "ftl_util.cc" ]
  public = [ "ftl_util.h" ]
  public_deps = [ "//zircon/public/lib/zx" ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/fxl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/inspect",
  ]
}

executable("ftl-proxy-bin") {
  output_name = "ftl-proxy"
  sources = [ "main.cc" ]
  deps = [
    ":ftl-util",
    ":local_storage_metrics",
    "//sdk/fidl/fuchsia.cobalt:fuchsia.cobalt_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/public/lib/fidl",
  ]
}

test("ftl-proxy-bin-test") {
  output_name = "ftl-proxy-test"
  testonly = true
  sources = [ "ftl_util_test.cc" ]
  deps = [
    ":ftl-util",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/async-testing",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/driver-integration-test",
    "//zircon/system/ulib/ramdevice-client",
  ]
}

fuchsia_component("ftl-proxy-component") {
  manifest = "meta/ftl-proxy.cmx"
  component_name = "ftl-proxy"
  deps = [ ":ftl-proxy-bin" ]
}

fuchsia_package("ftl-proxy") {
  deps = [ ":ftl-proxy-component" ]
}

isolated_devmgr_unittest_package("ftl-proxy-tests") {
  executable_path = "test/ftl-proxy-test"

  deps = [
    ":ftl-proxy-bin-test",
    "//src/devices/block/drivers/core",
    "//src/devices/block/drivers/ftl",
    "//src/devices/block/drivers/ramdisk",
    "//src/devices/bus/drivers/platform",
    "//src/devices/nand/drivers/ram-nand",
  ]
}
