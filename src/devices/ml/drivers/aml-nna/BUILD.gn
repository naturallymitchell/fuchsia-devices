# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//src/sys/build/components.gni")

bind_rules("aml_nna_bind") {
  rules = "aml_nna.bind"
  output = "aml_nna_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/amlogic.platform" ]
}

driver_module("aml-nna") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "aml-nna.cc" ]
  deps = [
    ":aml_nna_bind",
    "//sdk/banjo/fuchsia.hardware.platform.bus",
    "//sdk/banjo/fuchsia.hardware.registers",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_llcpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver",
    "//src/devices/lib/mmio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
  ]
}

test("aml-nna-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "aml-nna-test"
  sources = [
    "aml-nna-test.cc",
    "aml-nna.cc",
  ]
  deps = [
    ":aml_nna_bind",
    "//sdk/banjo/fuchsia.hardware.platform.bus",
    "//sdk/banjo/fuchsia.hardware.registers",
    "//sdk/banjo/fuchsia.hardware.registers:fuchsia.hardware.registers_mock",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_llcpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/mmio",
    "//src/devices/registers/testing/mock-registers",
    "//src/devices/testing/fake_ddk",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("aml-nna-test") {
  deps = [ ":aml-nna-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-nna-test",
    ":aml_nna_bind_test",
  ]
}
