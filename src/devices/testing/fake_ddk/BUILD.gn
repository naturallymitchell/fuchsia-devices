# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/unification/zx_library.gni")

declare_args() {
  # Enable printing of in driver logs in unittests.
  enable_driver_unittest_logs = true

  # Log levels to be printed when logs are enabled. Default is ERROR, WARNING, & INFO.
  # Refer to zircon/system/public/zircon/syscalls/log.h for levels.
  driver_unittest_log_flags = "0x7"
}

zx_library("fake_ddk") {
  sdk = "source"
  sdk_headers = [ "lib/fake_ddk/fake_ddk.h" ]
  testonly = true
  sources = [ "fake_ddk.cc" ]
  if (enable_driver_unittest_logs) {
    defines = [ "LOG_FLAGS=${driver_unittest_log_flags}" ]
  } else {
    defines = [ "LOG_FLAGS=0" ]
  }
  deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fidl-async",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
  public_deps = [
    # <lib/fake_ddk/fidl-helper.h> has #include <lib/async-loop/cpp/loop.h>.
    "//zircon/public/lib/async-loop",

    # <lib/fake_ddk/fake_ddk.h> has #include <ddk/device.h>.
    "//zircon/public/lib/ddk",

    # <lib/fake_ddk/fake_ddk.h> has #include <fbl/array.h>
    "//zircon/public/lib/fbl",

    # <lib/fake_ddk/fidl-helper.h> has #include <lib/fidl-async/bind.h>.
    "//zircon/public/lib/fidl-async",

    # <lib/fake_ddk/fake_ddk.h> has #include <lib/sync/completion.h>.
    "//zircon/public/lib/sync",
  ]
}
