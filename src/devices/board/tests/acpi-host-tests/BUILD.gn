# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/testing/host_test.gni")

# Our fake ACPICA OSL only compiles on Linux.
if (is_linux) {
  executable("acpi-host-test-bin") {
    testonly = true
    deps = [
      "//src/devices/board/lib/acpi:host-acpi",
      "//third_party/acpica",
      "//third_party/googletest:gtest",
    ]

    sources = [
      "acpi-host-test.cc",
      "acpi-stubs.cc",
      "fake-driver-env.cc",
      "table-manager.cc",
      "table.cc",
    ]
  }

  host_test("acpi-host-test") {
    binary_path = "$root_out_dir/acpi-host-test-bin"
    deps = [ ":acpi-host-test-bin" ]
    data_deps = [ "tables:test-data" ]

    # Pass the path to the test data into the test.
    args = [ rebase_path(get_label_info("tables:test-data", "target_out_dir"),
                         root_build_dir) ]
  }
} else {
  group("acpi-host-test") {
  }
}

group("tests") {
  testonly = true

  deps = [ ":acpi-host-test($host_toolchain)" ]
}
