# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = []
  if (target_cpu == "x64") {
    deps += [ ":x86-acpi-tests" ]
  }
}

shared_public_deps = [ "//src/devices/bind/fuchsia.acpi:bind.fuchsia.acpi" ]
shared_deps = [
  "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
  "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
  "//src/devices/lib/iommu",
  "//src/lib/ddk:ddk-deprecated-binding-headers",
  "//third_party/acpica",
  "//zircon/system/ulib/zxc",
]

source_set("acpi") {
  configs += [ "..:config" ]
  sources = [
    "acpi-impl.cc",
    "acpi.cc",
    "device.cc",
    "manager.cc",
    "resources.cc",
  ]
  public_deps = shared_public_deps
  deps = shared_deps + [
           "//src/lib/ddk",
           "//zircon/public/lib/fbl",
         ]
}

test("acpi-test") {
  sources = [
    "acpi.cc",
    "device.cc",
    "manager-test.cc",
    "manager.cc",
    "resources.cc",
    "status-test.cc",
    "util-test.cc",
  ]
  deps = shared_deps + shared_public_deps + [
           "//zircon/public/lib/zxtest",
           "//src/devices/testing/fake_ddk",
           "test:mock-acpi",
         ]
}

fuchsia_unittest_package("x86-acpi-tests") {
  deps = [ ":acpi-test" ]
}
