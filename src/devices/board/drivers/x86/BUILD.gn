# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")
import("//src/devices/bus/drivers/pci/pci.gni")

declare_args() {
  # If true, power on the Atlas camera at boot.
  # TODO(fxbug.dev/81684): remove once we have a better way to manage ACPI device power.
  i_can_haz_atlas_camera = false
}

group("x86") {
  deps = [ ":platform-bus-x86" ]
}

config("config") {
  visibility = [
    ":*",
    "acpi/*",
  ]

  include_dirs = [
    "include",
    ".",
  ]

  if (platform_enable_user_pci) {
    defines = [ "ENABLE_USER_PCI" ]
  }

  cflags = [ "-Wno-null-pointer-arithmetic" ]
  cflags_c = [ "-fno-strict-aliasing" ]
}

source_set("smbios") {
  visibility = [ ":*" ]

  sources = [ "smbios.cc" ]

  configs += [ ":config" ]

  deps = [
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/smbios",
    "//zircon/system/ulib/zx",
  ]
}

driver_bind_rules("x64-bind") {
  rules = "x64.bind"
  header_output = "x64-bind.h"
  bind_output = "platform-bus-x64.bindbc"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.intel.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("goldfish_control_2_bind") {
  rules = "bind/goldfish_control_2.bind"
  header_output = "goldfish_control_2_bind.h"
  bind_output = "goldfish_control_2_bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.goldfish",
    "//src/devices/bind/fuchsia.google.platform",
  ]
}

fuchsia_driver("platform-bus-x86-driver") {
  output_name = "platform-bus-x86"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  if (i_can_haz_atlas_camera) {
    cflags = [ "-DENABLE_ATLAS_CAMERA" ]
  }

  sources = [
    "acpi-dev/dev-ec.cc",
    "acpi-dev/dev-lid.cc",
    "acpi-dev/dev-lid.h",
    "acpi-dev/dev-pwrbtn.cc",
    "acpi-dev/dev-tbmc.cc",
    "acpi-nswalk.cc",
    "debug.cc",
    "goldfish-control.cc",
    "init.cc",
    "iommu-driver-tests.cc",
    "methods.cc",
    "pciroot.cc",
    "power.cc",
    "sysmem.cc",
    "x86-driver-tests.cc",
    "x86.cc",
    "x86.h",
  ]

  # Userspace PCI feature flag.  The scaffolding for the userspace pci bus
  # driver is in pci.cpp.  If not enabled then the kernel pci bus driver
  # initialization code in ACPI (kpci.cc) will be built instead.
  if (platform_enable_user_pci) {
    sources += [
      "pci.cc",
      "pci_irqs.cc",
    ]
  } else {
    sources += [ "kpci.cc" ]
  }
  configs += [ ":config" ]
  deps = [
    ":goldfish_control_2_bind_header",
    ":smbios",
    ":x64-bind",
    "acpi",
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.pciroot:fuchsia.hardware.pciroot_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_c",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_llcpp",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_c",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_llcpp",
    "//sdk/fidl/fuchsia.sysinfo:fuchsia.sysinfo_c",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
    "//src/devices/lib/driver",
    "//src/devices/lib/iommu",
    "//src/devices/pci/lib/pci",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//sdk/lib/fit",
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/lib/ddktl",
    "//third_party/acpica",
    "//zircon/system/ulib/driver-unit-test",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/lazy_init",
    "//zircon/system/ulib/region-alloc",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxc",
    "//zircon/system/ulib/zxtest",
    "//zircon/third_party/ulib/chromiumos-platform-ec",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("platform-bus-x86") {
  component_name = "platform-bus-x86"
  deps = [ ":platform-bus-x86-driver" ]
  info = "platform-bus-x86-info.json"
}

test("x86-lid-test") {
  sources = [
    "acpi-dev/dev-lid-test.cc",
    "acpi-dev/dev-lid.cc",
    "acpi-dev/dev-lid.h",
  ]
  configs += [ ":config" ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.acpi:fuchsia.hardware.acpi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_llcpp",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/ui/input/testing/mock-hidbus-ifc",
    "//third_party/acpica",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zxtest",
  ]
}

bootfs_test("x86-lid-bootfs-test") {
  name = "x86-lid-test"
  deps = [ ":x86-lid-test" ]
}

test("x86-board-test") {
  sources = [ "smbios-test.cc" ]
  configs += [ ":config" ]
  deps = [
    ":smbios",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

bootfs_test("x86-board-bootfs-test") {
  name = "x86-board-test"
  deps = [ ":x86-board-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":x64-bind_test",
    "acpi:tests",
  ]
}
