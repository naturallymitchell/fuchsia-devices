# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

config("factory") {
  defines = [ "FACTORY_BUILD" ]
}

config("rndis") {
  defines = [ "ENABLE_RNDIS=1" ]
}

driver_bind_rules("sherlock-bind") {
  rules = "sherlock.bind"
  header_output = "sherlock-bind.h"
  bind_output = "sherlock.bindbc"
  tests = "sherlock-bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("luis-bind") {
  rules = "luis.bind"
  header_output = "luis-bind.h"
  bind_output = "luis.bindbc"
  tests = "luis-bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-buttons-bind") {
  rules = "bind/sherlock-buttons.bind"
  header_output = "sherlock-buttons-bind.h"
  bind_output = "sherlock-buttons-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.gpio",
  ]
}

driver_bind_rules("sherlock-pwm-bind") {
  rules = "bind/sherlock-pwm.bind"
  header_output = "sherlock-pwm-bind.h"
  bind_output = "sherlock-pwm-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

driver_bind_rules("sherlock-backlight-bind") {
  rules = "bind/sherlock-backlight.bind"
  header_output = "sherlock-backlight-bind.h"
  bind_output = "sherlock-backlight-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.i2c",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.texasinstruments.platform",
  ]
}

driver_bind_rules("sherlock-display-bind") {
  rules = "bind/sherlock-display.bind"
  header_output = "sherlock-display-bind.h"
  bind_output = "sherlock-display_bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.dsi",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-mali-bind") {
  rules = "bind/sherlock-mali.bind"
  header_output = "sherlock-mali-bind.h"
  bind_output = "sherlock-mali-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.registers",
  ]
}

driver_bind_rules("sherlock-spi-bind") {
  rules = "bind/sherlock-spi.bind"
  header_output = "sherlock-spi-bind.h"
  bind_output = "sherlock-spi-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-tee-bind") {
  rules = "bind/sherlock-tee.bind"
  header_output = "sherlock-tee-bind.h"
  bind_output = "sherlock-tee-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.rpmb",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-bluetooth-bind") {
  rules = "bind/sherlock-bluetooth.bind"
  header_output = "sherlock-bluetooth-bind.h"
  bind_output = "sherlock-bluetooth-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

driver_bind_rules("sherlock-video-bind") {
  rules = "bind/sherlock-video.bind"
  header_output = "sherlock-video-bind.h"
  bind_output = "sherlock-video-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
    "//src/devices/bind/fuchsia.tee",
  ]
}

driver_bind_rules("sherlock-video-enc-bind") {
  rules = "bind/sherlock-video-enc.bind"
  header_output = "sherlock-video-enc-bind.h"
  bind_output = "sherlock-video-enc-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-hevc-enc-bind") {
  rules = "bind/sherlock-hevc-enc.bind"
  header_output = "sherlock-hevc-enc-bind.h"
  bind_output = "sherlock-hevc-enc-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("luis-cpu-bind") {
  rules = "bind/luis-cpu.bind"
  header_output = "luis-cpu-bind.h"
  bind_output = "luis-cpu-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.clock",
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.power",
    "//src/devices/bind/fuchsia.thermal",
  ]
}

driver_bind_rules("sherlock-cpu-bind") {
  rules = "bind/sherlock-cpu.bind"
  header_output = "sherlock-cpu-bind.h"
  bind_output = "sherlock-cpu-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.thermal",
  ]
}

template("sherlock_driver") {
  driver_module(target_name + "-driver") {
    output_name = invoker.target_name
    forward_variables_from(invoker, "*")

    configs += [
      "//build/config:all_source",
      "//build/config/fuchsia:enable_zircon_asserts",
    ]
    if (use_factory) {
      configs += [ ":factory" ]
    }
    if (enable_rndis) {
      configs += [ ":rndis" ]
    }

    sources = [
      "luis-cpu.cc",
      "luis-power.cc",
      "luis-thermal.cc",
      "sherlock-audio.cc",
      "sherlock-backlight.cc",
      "sherlock-bluetooth.cc",
      "sherlock-board.cc",
      "sherlock-buttons.cc",
      "sherlock-camera.cc",
      "sherlock-canvas.cc",
      "sherlock-clk.cc",
      "sherlock-cpu.cc",
      "sherlock-display.cc",
      "sherlock-dsi.cc",
      "sherlock-emmc.cc",
      "sherlock-gpio.cc",
      "sherlock-hevc-enc.cc",
      "sherlock-i2c.cc",
      "sherlock-light.cc",
      "sherlock-mali.cc",
      "sherlock-nna.cc",
      "sherlock-ot-radio.cc",
      "sherlock-pwm.cc",
      "sherlock-ram.cc",
      "sherlock-registers.cc",
      "sherlock-sdio.cc",
      "sherlock-securemem.cc",
      "sherlock-spi.cc",
      "sherlock-sysmem.cc",
      "sherlock-tee.cc",
      "sherlock-thermal.cc",
      "sherlock-thermistor.cc",
      "sherlock-touch.cc",
      "sherlock-usb.cc",
      "sherlock-video-enc.cc",
      "sherlock-video.cc",
      "sherlock.cc",
    ]

    deps = [
      ":luis-cpu-bind_header",
      ":sherlock-backlight-bind_header",
      ":sherlock-bluetooth-bind_header",
      ":sherlock-buttons-bind_header",
      ":sherlock-cpu-bind_header",
      ":sherlock-display-bind_header",
      ":sherlock-hevc-enc-bind_header",
      ":sherlock-mali-bind_header",
      ":sherlock-pwm-bind_header",
      ":sherlock-spi-bind_header",
      ":sherlock-tee-bind_header",
      ":sherlock-video-bind_header",
      ":sherlock-video-enc-bind_header",
      "//sdk/banjo/fuchsia.hardware.clock:fuchsia.hardware.clock_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.iommu:fuchsia.hardware.iommu_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
      "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_c",
      "//sdk/fidl/fuchsia.hardware.gpu.amlogic:fuchsia.hardware.gpu.amlogic_llcpp",
      "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_c",
      "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
      "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_c",
      "//src/connectivity/openthread/lib/ot-radio",
      "//src/devices/bind/fuchsia.ams.platform:bind.fuchsia.ams.platform",
      "//src/devices/lib/amlogic",
      "//src/devices/lib/broadcom",
      "//src/devices/lib/driver",
      "//src/devices/lib/fidl-metadata",
      "//src/devices/lib/focaltech",
      "//src/devices/lib/metadata:llcpp",
      "//src/devices/lib/mmio",
      "//src/devices/lib/thermal",
      "//src/devices/lib/ti",
      "//src/devices/usb/lib/usb",
      "//src/graphics/display/lib/device-protocol-display",
      "//src/lib/ddk",
      "//src/lib/ddk:ddk-metadata-headers",

      # TODO(fxb/38132): Migrate to the new bind rules and delete the below
      "//src/lib/ddk:ddk-deprecated-binding-headers",
      "//src/lib/ddktl",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/hwreg",
      "//zircon/public/lib/zircon-internal",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/inspect",
    ]

    # We need both headers to build but we can only depend on one bindfile at a time.
    deps += [
      ":luis-bind_header",
      ":sherlock-bind_header",
    ]

    if (defined(is_luis) && is_luis) {
      defines = [ "IS_LUIS=1" ]
      deps += [ ":luis-bind" ]
    } else {
      deps += [ ":sherlock-bind" ]
    }
  }

  fuchsia_driver_component(target_name) {
    deps = [ ":${target_name}-driver" ]
  }
}

sherlock_driver("sherlock") {
  use_factory = false
  enable_rndis = false
}

sherlock_driver("sherlock-factory") {
  use_factory = true
  enable_rndis = false
}

sherlock_driver("sherlock-factory-rndis") {
  use_factory = true
  enable_rndis = true
}

sherlock_driver("luis") {
  use_factory = false
  enable_rndis = false
  is_luis = true
}

sherlock_driver("luis-factory") {
  use_factory = true
  enable_rndis = false
  is_luis = true
}

sherlock_driver("luis-factory-rndis") {
  use_factory = true
  enable_rndis = true
  is_luis = true
}

group("tests") {
  testonly = true
  deps = [
    ":luis-bind_test",
    ":sherlock-bind_test",
  ]
}
