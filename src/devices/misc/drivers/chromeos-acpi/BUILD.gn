# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("chromeos-acpi") {
  deps = [ ":pkg" ]
}

group("tests") {
  testonly = true
  deps = [
    ":chromeos-acpi-tests",
    ":chromeos_acpi_bind_test",
  ]
}

driver_bind_rules("chromeos_acpi_bind") {
  rules = "chromeos_acpi.bind"
  header_output = "chromeos_acpi-bind.h"
  bind_output = "chromeos_acpi.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":chromeos_acpi_bind",
  "//sdk/fidl/fuchsia.acpi.chromeos:fuchsia.acpi.chromeos_llcpp",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver",
  "//src/lib/ddktl",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("driver") {
  output_name = "chromeos-acpi"
  sources = [ "chromeos_acpi.cc" ]
  deps = common_deps
}

fuchsia_driver_component("component") {
  component_name = "chromeos-acpi"
  deps = [ ":driver" ]
  info = "component-info.json"
}

fuchsia_system_package("pkg") {
  package_name = "chromeos-acpi"
  deps = [ ":component" ]

  # Remove the line below when this package is only used by the system image.
}

test("chromeos-acpi-tests-bin") {
  output_name = "chromeos-acpi-tests"
  sources = [
    "chromeos_acpi.cc",
    "unit-tests.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/testing/mock-ddk",
           "//src/devices/lib/acpi/mock:mock-acpi",
           "//zircon/system/ulib/zxtest",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
         ]
}

fuchsia_unittest_package("chromeos-acpi-tests") {
  deps = [ ":chromeos-acpi-tests-bin" ]
}
