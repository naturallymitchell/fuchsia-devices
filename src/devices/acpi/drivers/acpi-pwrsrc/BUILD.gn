# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":acpi-pwrsrc-tests" ]
}

driver_bind_rules("bind") {
  rules = "acpi_pwrsrc.bind"
  header_output = "acpi_pwrsrc-bind.h"
  bind_output = "acpi_pwrsrc.bindbc"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":bind",
  "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_llcpp",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver",
  "//src/lib/ddktl",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("driver") {
  output_name = "acpi-pwrsrc"
  sources = [ "acpi_pwrsrc.cc" ]
  deps = common_deps
}

fuchsia_driver_component("component") {
  component_name = "acpi-pwrsrc"
  deps = [ ":driver" ]
  info = "component-info.json"
}

fuchsia_system_package("acpi-pwrsrc") {
  deps = [ ":component" ]

  # Remove the line below when this package is only used by the system image.
  allowed_in_extra_deps = true
}

test("acpi-pwrsrc-unit-tests") {
  sources = [
    "acpi_pwrsrc.cc",
    "acpi_pwrsrc.h",
    "unit-tests.cc",
  ]

  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/zxtest",
           "//src/devices/lib/acpi/mock:mock-acpi",
         ]
}

fuchsia_unittest_package("acpi-pwrsrc-tests") {
  deps = [ ":acpi-pwrsrc-unit-tests" ]
}
