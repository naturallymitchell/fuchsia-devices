# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("i2c_bind") {
  rules = "i2c.bind"
  header_output = "i2c_bind.h"
  bind_output = "i2c.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

driver_module("i2c-driver") {
  output_name = "i2c"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "i2c-bus.cc",
    "i2c-child.cc",
    "i2c.cc",
  ]
  deps = [
    ":i2c_bind",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//src/lib/listnode",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace:trace-driver",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("i2c") {
  component_name = "i2c"
  deps = [ ":i2c-driver" ]
}

test("i2c-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "i2c-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "i2c-bus.cc",
    "i2c-child-test.cc",
    "i2c-child.cc",
    "i2c-test.cc",
    "i2c.cc",
  ]
  deps = [
    ":i2c_bind",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//src/lib/listnode",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace:trace-driver",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("i2c-test") {
  deps = [ ":i2c-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":i2c-test" ]
}
