# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/unification/zbi/bootfs_test.gni")

group("test") {
  testonly = true
  deps = [ ":device-protocol-pdev-test" ]
}

test("device-protocol-pdev-test") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "device-protocol-pdev-test"
  sources = [ "pdev-test.cc" ]
  public_deps = [
    # <lib/device-protocol/pdev.h> has #include <ddktl/protocol/platform/device.h>.
    "//sdk/banjo/ddk.protocol.platform.device",

    # Banjo protocols have #include <ddk/driver.h>.
    "//src/lib/ddk",

    # Banjo protocols have #include <ddktl/device-internal.h>.
    "//src/lib/ddktl",
  ]

  deps = [
    # pdev.cpp has #include <lib/mmio/mmio.h>.
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zxtest",
  ]
}

bootfs_test("device-protocol-pdev-test-bootfs-test") {
  name = "device-protocol-pdev-test"
  deps = [ ":device-protocol-pdev-test" ]
}
