# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
import("//build/components.gni")

group("common") {
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.radar:fuchsia.hardware.radar_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
  ]
}

executable("radarutil-bin") {
  output_name = "radarutil"
  configs += [ "//build/config:all_source" ]
  sources = [
    "main.cc",
    "radarutil.cc",
  ]
  deps = [ ":common" ]
}

fuchsia_component("radarutil-component") {
  component_name = "radarutil"
  manifest = "meta/radarutil.cmx"
  deps = [ ":radarutil-bin" ]
}

fuchsia_package("radarutil") {
  deps = [ ":radarutil-component" ]
}

executable("radarutil-test-bin") {
  testonly = true
  configs += [ "//build/config:all_source" ]
  sources = [
    "radarutil-test.cc",
    "radarutil.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/zxtest",
  ]
}

fuchsia_unittest_package("radarutil-test") {
  deps = [ ":radarutil-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [ ":radarutil-test" ]
}
