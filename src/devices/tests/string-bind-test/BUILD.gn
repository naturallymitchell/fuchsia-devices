# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

test("test") {
  output_name = "string-bind-test"

  sources = [ "string-bind-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.device.test:fuchsia.device.test_llcpp",
    "//sdk/fidl/fuchsia.driver.development",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/devmgr-integration-test",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

isolated_devmgr_unittest_package("string-bind-test") {
  executable_path = "test/string-bind-test"

  deps = [
    ":child-driver",
    ":parent-driver",
    ":test",
    "//src/devices/misc/drivers/test",
    "//src/devices/misc/drivers/test-parent",
  ]
}

bind_library("stringbind.lib") {
  source = "stringbindlib.bind"
}

driver_bind_rules("parent-driver-bind") {
  rules = "parent-driver.bind"
  header_output = "parent-driver-bind.h"
  disable_autobind = true
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

driver_module("parent-driver-driver") {
  output_name = "string-bind-parent"
  test = true
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "parent-driver.cc" ]
  deps = [
    ":parent-driver-bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("parent-driver") {
  component_name = "string-bind-parent"
  deps = [ ":parent-driver-driver" ]
}

driver_bind_rules("child-driver-bind") {
  rules = "child-driver.bind"
  header_output = "child-driver-bind.h"
  deps = [
    ":stringbind.lib",
    "//src/devices/bind/fuchsia.test",
  ]
}

driver_module("child-driver-driver") {
  output_name = "string-bind-child"
  test = true
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "child-driver.cc" ]
  deps = [
    ":child-driver-bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("child-driver") {
  component_name = "string-bind-child"
  deps = [ ":child-driver-driver" ]
}
