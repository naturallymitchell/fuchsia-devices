# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/images/manifest.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

executable("tests") {
  testonly = true

  output_name = "libdriver-integration-test"

  sources = [
    "action-list.cc",
    "basic-lifecycle-test.cc",
    "composite-device-test.cc",
    "device-add-tests.cc",
    "integration-test.cc",
    "mock-device-hooks.cc",
    "mock-device-thread.cc",
    "mock-device.cc",
    "root-mock-device.cc",
  ]

  deps = [
    ":mock-device",
    "//garnet/public/lib/gtest",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/devmgr-integration-test",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/inspector",
    "//zircon/system/fidl/fuchsia-device:fuchsia-device",
    "//zircon/system/fidl/fuchsia-device-test:fuchsia-device-test",
    "//zircon/system/fidl/fuchsia-device-test:fuchsia-device-test_c",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io",
  ]
}

action("devmgr-manifest") {
  visibility = [ ":*" ]
  sources = zircon_boot_manifests
  outputs = [
    "$target_gen_dir/devmgr.manifest",
  ]
  script = "//build/images/manifest.py"
  args = [
    "--include=bin/devcoordinator",
    "--include=bin/fshost",
    "--output=" + rebase_path(outputs[0], root_build_dir),
  ]
  foreach(manifest, sources) {
    args += [
      "--cwd=" + rebase_path(get_path_info(manifest, "dir"), root_build_dir),
      "--manifest=" + rebase_path(manifest, root_build_dir),
    ]
  }
}

test_package("libdriver-integration-test") {
  deprecated_shell = "//build"

  deps = [
    ":devmgr-manifest",
    ":tests",
  ]

  extra = [ "$target_gen_dir/devmgr.manifest" ]

  tests = [
    {
      name = "libdriver-integration-test"
      environments = basic_envs
    },
  ]
}

fidl("mock-device") {
  # TODO(fxb/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "event-names-must-start-with-on" ]

  name = "fuchsia.device.mock"

  sources = [
    "//zircon/system/dev/test/mock-device/mock-device.fidl",
  ]

  public_deps = []
}
