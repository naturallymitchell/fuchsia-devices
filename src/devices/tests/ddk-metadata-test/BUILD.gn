# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

driver_bind_rules("metadata-test-bind") {
  disable_autobind = true
  header_output = "metadata-test-bind.h"
}

driver_module("driver_module") {
  output_name = "ddk-metadata-test-driver"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "metadata-test.cc" ]
  deps = [
    ":metadata-test-bind",
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]

  visibility = [
    ":*",
    "//src/devices/bundles:drivers",
  ]
}

fuchsia_driver_component("driver") {
  component_name = "ddk-metadata-test-driver"
  deps = [ ":driver_module" ]
}

test("tests") {
  output_name = "ddk-metadata-test"

  testonly = true
  sources = [ "test.cc" ]
  deps = [
    ":metadata-test-bind",
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/fbl",
  ]
  visibility = [ ":*" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

isolated_devmgr_unittest_package("ddk-metadata-test") {
  executable_path = "test/ddk-metadata-test"

  deps = [
    ":driver",
    ":tests",
    "//src/devices/misc/drivers/test",
    "//src/devices/misc/drivers/test-parent",
  ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
