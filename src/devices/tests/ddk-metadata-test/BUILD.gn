# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

driver_module("driver") {
  output_name = "ddk-metadata-test"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "metadata-test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]

  visibility = [
    ":*",
    "//src/devices/bundles:drivers",
  ]
}

test("tests") {
  output_name = "ddk-metadata-test"

  testonly = true
  sources = [ "test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
  ]
  visibility = [ ":*" ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

isolated_devmgr_unittest_package("ddk-metadata-test") {
  executable_path = "test/ddk-metadata-test"

  deps = [
    ":driver",
    ":tests",
    "//src/devices/misc/drivers/test",
    "//src/devices/tests/sysdev",
  ]
}
