# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

driver_bind_rules("test-environment-bind") {
  rules = "test-environment.bind"
  header_output = "test-environment-bind.h"
  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fidl("fuchsia.device.environment.test") {
  sources = [ "environment.test.fidl" ]
}

fuchsia_driver("ddk-environment-test-driver") {
  output_name = "ddk-environment-test"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test-driver.cc" ]
  deps = [
    ":fuchsia.device.environment.test_llcpp",
    ":test-environment-bind",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/files",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_driver_component("ddk-environment-test") {
  component_name = "ddk-environment-test"
  deps = [ ":ddk-environment-test-driver" ]
}

test("ddk-environment") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.environment.test_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//zircon/system/ulib/driver-integration-test",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("ddk-environment-test-package") {
  deps = [
    ":ddk-environment",
    ":ddk-environment-test",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ddk-environment-test-package" ]
}
