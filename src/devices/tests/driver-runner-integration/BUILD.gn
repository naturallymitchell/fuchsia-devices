# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":driver-runner-integration-test" ]
}

rustc_test("driver_runner_integration_test") {
  testonly = true
  source_root = "driver_runner_integration_test.rs"
  sources = [ "driver_runner_integration_test.rs" ]
  deps = [
    "//src/lib/fuchsia-async",
    "//src/sys/lib/component-events",
    "//src/sys/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_component("driver-runner-integration-test-component") {
  testonly = true
  manifest = "meta/driver-runner-integration-test.cml"
  deps = [ ":driver_runner_integration_test" ]
}

fuchsia_component("driver-runner-integration-root") {
  manifest = "meta/driver-runner-integration-root.cml"
}

fuchsia_component("driver-manager-component") {
  manifest = "meta/driver-runner-integration-driver-manager.cml"
  deps = [
    "//src/devices/bin/driver_host:driver_host",
    "//src/devices/bin/driver_manager:driver_manager",
    "//src/devices/bus/drivers/platform",
  ]
}

bind_index_metadata("driver_bind_manifest") {
  testonly = true
  deps = []
  outputs = [ "config/driver-index.json" ]
}

fuchsia_test_package("driver-runner-integration-test") {
  test_components = [ ":driver-runner-integration-test-component" ]
  deps = [
    ":driver-manager-component",
    ":driver-runner-integration-root",
    ":driver_bind_manifest",
    "//src/devices/bin/driver-index:component",
    "//src/devices/bin/driver_host2",
    "//src/devices/misc/drivers/packaged:component",
    "//src/lib/isolated_devmgr/v2_component:driver-manager-support",
  ]
}
