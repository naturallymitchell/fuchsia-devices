# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

fidl("fuchsia.device.lifecycle.test") {
  sources = [ "lifecycle.test.fidl" ]
}

driver_module("ddk-lifecycle-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "test-driver-child.cc",
    "test-driver.cc",
  ]
  deps = [
    ":fuchsia.device.lifecycle.test_llcpp",
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/sync",
  ]

}

test("ddk-lifecycle") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.lifecycle.test_llcpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/devmgr-integration-test",
    "//zircon/public/lib/devmgr-launcher",
    "//zircon/public/lib/driver-integration-test",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

migrated_manifest("ddk-lifecycle-test-manifest") {
  deps = [
    ":ddk-lifecycle-test",
  ]
}

migrated_manifest("ddk-lifecycle-manifest") {
  deps = [
    ":ddk-lifecycle",
  ]
}
