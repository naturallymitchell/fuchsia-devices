# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

fidl("fuchsia.device.schedule.work.test") {
  sources = [ "schedule-work.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

driver_module("ddk-schedule-work-test") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [ "test-driver.cc" ]
  deps = [
    ":fuchsia.device.schedule.work.test_llcpp",
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
  ]
}

test("ddk-schedule-work") {
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.schedule.work.test_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/driver-integration-test",
  ]
}

isolated_devmgr_unittest_package("ddk-schedule-work-test-package") {
  package_name = "ddk-schedule-work-test"
  executable_path = "test/ddk-schedule-work"
  deps = [
    ":ddk-schedule-work",
    ":ddk-schedule-work-test",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
    "//src/devices/tests/sysdev",
  ]
}

executable("ddk-schedule-work-perf") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "perf-test.cc" ]
  deps = [
    ":fuchsia.device.schedule.work.test_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
    "//zircon/system/ulib/driver-integration-test",
  ]
}

isolated_devmgr_unittest_package("ddk-schedule-work-perf-package") {
  package_name = "ddk-schedule-work-perf"
  executable_path = "bin/ddk-schedule-work-perf"
  deps = [
    ":ddk-schedule-work-perf",
    ":ddk-schedule-work-test",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
    "//src/devices/tests/sysdev",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ddk-schedule-work-perf-package",
    ":ddk-schedule-work-test-package",
  ]
}
