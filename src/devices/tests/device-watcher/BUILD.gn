# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")
import("//src/lib/isolated_devmgr/v2_component/isolated_devmgr.gni")

driver_bind_rules("test-bind") {
  rules = "test-driver.bind"
  header_output = "test-driver-bind.h"

  deps = [ "//src/devices/bind/fuchsia.test" ]
}

driver_module("test-driver-driver") {
  output_name = "test-driver"
  sources = [ "test-driver.cc" ]
  deps = [
    ":test-bind",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_llcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/files",
    "//zircon/public/lib/sync",
  ]
}

fuchsia_driver_component("test-driver") {
  component_name = "test-driver"
  deps = [ ":test-driver-driver" ]
}

test("test") {
  output_name = "device-watcher-test"
  sources = [ "test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.device.manager:fuchsia.device.manager_llcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/service:service-llcpp",
  ]
}

fuchsia_component("device-watcher-test") {
  testonly = true
  manifest = "meta/device-watcher-test.cml"
  deps = [ ":test" ]
}

fuchsia_component("device-watcher-realm") {
  testonly = true
  manifest = "meta/device-watcher-realm.cml"
}

isolated_devmgr_v2_component("devmgr") {
  deps = [ ":test-driver" ]
}

fuchsia_test_package("device-watcher-test-package") {
  package_name = "device-watcher-test"
  test_components = [ ":device-watcher-realm" ]
  deps = [
    ":device-watcher-test",
    ":devmgr",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":device-watcher-test-package" ]
}
