# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

test("test") {
  output_name = "bind-test"

  sources = [ "bind-test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_llcpp",
    "//sdk/fidl/fuchsia.device.test:fuchsia.device.test_llcpp",
    "//sdk/fidl/fuchsia.driver.development",
    "//sdk/lib/fdio",
    "//sdk/lib/sys/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/devmgr-integration-test",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
  ]
}

isolated_devmgr_unittest_package("bind-test") {
  executable_path = "test/bind-test"

  deps = [
    ":bind-test-driver",
    ":test",
    "//src/devices/misc/drivers/test",
    "//src/devices/misc/drivers/test-parent",
  ]
}

driver_bind_rules("bind") {
  rules = "bind-test.bind"
  header_output = "bind-test-bind.h"
  bind_output = "bind-test-bind.bindbc"
  disable_autobind = true
  disable_new_bytecode = true
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

driver_module("bind-test-driver-driver") {
  output_name = "bind-test-driver"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  sources = [ "bind-test-driver.cc" ]
  deps = [
    ":bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("bind-test-driver") {
  component_name = "bind-test-driver"
  deps = [ ":bind-test-driver-driver" ]
}
