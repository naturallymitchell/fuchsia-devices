# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("spi_bind") {
  rules = "spi.bind"
  header_output = "spi_bind.h"
  bind_output = "spi.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.spi" ]
}

group("tests") {
  testonly = true
  deps = [
    ":spi-test",
    ":spi_bind_test",
  ]
}

driver_module("spi-driver") {
  output_name = "spi"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "spi-child.cc",
    "spi.cc",
  ]
  deps = [
    ":spi_bind",
    "//sdk/banjo/fuchsia.hardware.spi:fuchsia.hardware.spi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_llcpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl:fidl-llcpp",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/trace:trace-driver",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("spi") {
  component_name = "spi"
  deps = [ ":spi-driver" ]
}

test("spi-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "spi-test"
  sources = [
    "spi-child.cc",
    "spi-test.cc",
    "spi.cc",
  ]
  deps = [
    ":spi_bind",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.spi:fuchsia.hardware.spi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_llcpp",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl:fidl-llcpp",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/spi",
    "//zircon/system/ulib/trace:trace-driver",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("spi-test") {
  deps = [ ":spi-test-bin" ]
}
