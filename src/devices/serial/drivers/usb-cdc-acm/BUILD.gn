# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")
import("//zircon/system/ulib/devmgr-integration-test/isolated_devmgr.gni")

driver_bind_rules("cdc_acm_bind") {
  rules = "cdc_acm.bind"
  header_output = "cdc_acm_bind.h"
  bind_output = "usb-cdc-acm.bindbc"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.comm",
  ]
}

driver_module("usb-cdc-acm-driver") {
  output_name = "usb-cdc-acm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "usb-cdc-acm.cc" ]
  deps = [
    ":cdc_acm_bind",
    "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_c",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("usb-cdc-acm") {
  component_name = "usb-cdc-acm"
  deps = [ ":usb-cdc-acm-driver" ]
}

driver_bind_rules("usb_cdc_acm_function_bind") {
  rules = "usb_cdc_acm_function.bind"
  header_output = "usb_cdc_acm_function_bind.h"
  tests = "function_bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.comm",
  ]
}

driver_module("fake-usb-cdc-acm-driver") {
  output_name = "fake-usb-cdc-acm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "usb-cdc-acm-function.cc" ]
  deps = [
    ":usb_cdc_acm_function_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("fake-usb-cdc-acm") {
  component_name = "fake-usb-cdc-acm"
  deps = [ ":fake-usb-cdc-acm-driver" ]
}

test("usb-cdc-acm-integration") {
  output_name = "usb-cdc-acm-integration-test"
  sources = [ "usb-cdc-acm-test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.usb.peripheral:fuchsia.hardware.usb.peripheral_llcpp",
    "//sdk/fidl/fuchsia.hardware.usb.virtual.bus:fuchsia.hardware.usb.virtual.bus_llcpp",
    "//sdk/lib/fdio",
    "//src/lib/ddk",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/usb-virtual-bus-launcher",
    "//zircon/system/ulib/zxtest",
  ]
}

isolated_devmgr_unittest_package("usb-cdc-acm-integration-test") {
  executable_path = "test/usb-cdc-acm-integration-test"
  deps = [
    ":fake-usb-cdc-acm",
    ":usb-cdc-acm",
    ":usb-cdc-acm-integration",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
    "//src/devices/serial/drivers/serial",
    "//src/devices/usb/drivers/usb-bus",
    "//src/devices/usb/drivers/usb-composite",
    "//src/devices/usb/drivers/usb-peripheral",
    "//src/devices/usb/drivers/usb-virtual-bus",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":cdc_acm_bind_test",
    ":usb-cdc-acm-integration-test",
    ":usb_cdc_acm_function_bind_test",
  ]
}
