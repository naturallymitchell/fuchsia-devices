# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")

driver_bind_rules("serial_bind") {
  rules = "serial.bind"
  header_output = "serial_bind.h"
  bind_output = "serial.bindbc"
  tests = "tests/tests.json"
  deps = [ "//src/devices/bind/fuchsia.serial" ]
}

driver_module("serial-driver") {
  output_name = "serial"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("serial") {
  component_name = "serial"
  deps = [ ":serial-driver" ]
}

source_set("common") {
  configs += [ "//build/config:all_source" ]
  sources = [ "serial.cc" ]
  public_deps = [
    ":serial_bind",
    "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.serialimpl:fuchsia.hardware.serialimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_c",
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_llcpp",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/zx",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":serial_bind_test",
    "tests",
  ]
}
