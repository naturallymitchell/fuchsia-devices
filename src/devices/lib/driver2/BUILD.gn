# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("driver2") {
  public_deps = [ ":lib" ]
}

group("tests") {
  testonly = true
  deps = [ ":driver2-tests" ]
}

source_set("lib") {
  sources = [ "record.h" ]

  public_deps = [ "//zircon/system/ulib/async" ]
}

source_set("llcpp") {
  sources = [
    "devfs_exporter.cc",
    "devfs_exporter.h",
    "inspect.cc",
    "inspect.h",
    "logger.cc",
    "logger.h",
    "logger_internal.h",
    "namespace.cc",
    "namespace.h",
    "promise.h",
    "start_args.h",
    "structured_logger.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_llcpp",
    "//sdk/fidl/fuchsia.data:fuchsia.data_llcpp",
    "//sdk/fidl/fuchsia.device.fs:fuchsia.device.fs_llcpp",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework_llcpp",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_llcpp",
    "//sdk/lib/fdio",
    "//sdk/lib/fit-promise",
    "//sdk/lib/syslog/structured_backend",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zxc",
  ]
}

executable("llcpp_test") {
  testonly = true
  sources = [
    "devfs_exporter_test.cc",
    "logger_test.cc",
    "namespace_test.cc",
    "start_args_test.cc",
    "test_base.cc",
    "test_base.h",
  ]

  deps = [
    ":llcpp",
    "//sdk/fidl/fuchsia.device.fs",
    "//sdk/fidl/fuchsia.io",
    "//sdk/fidl/fuchsia.logger",
    "//sdk/lib/fidl/cpp",
    "//src/diagnostics/lib/cpp-log-decoder:lib",
    "//src/lib/diagnostics/accessor2logger",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/rapidjson",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/svc",
  ]
}

fuchsia_unittest_component("driver2-llcpp-test") {
  manifest = "meta/llcpp_test.cml"

  deps = [ ":llcpp_test" ]
}

fuchsia_test_package("driver2-tests") {
  test_components = [ ":driver2-llcpp-test" ]
}
