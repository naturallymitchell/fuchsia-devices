# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//src/sys/build/components.gni")

group("tests") {
  testonly = true

  deps = [ ":register-util-test-package" ]
}

test("bin") {
  testonly = true
  output_name = "register-util-test"
  visibility = [ ":*" ]

  sources = [
    "register-util-test.cc",
    "register-util.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/runtime",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_unittest_package("register-util-test-package") {
  manifest = "meta/register-util-test.cmx"
  deps = [ ":bin" ]
}

executable("register-util") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "main.cc",
    "register-util.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_llcpp",
    "//sdk/lib/fdio",
    "//zircon/public/lib/pretty",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]
}

fuchsia_shell_package("register-util-package") {
  package_name = "register-util"
  deps = [ ":register-util" ]
}
