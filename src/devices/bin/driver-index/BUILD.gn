# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers/driver_manifest.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/drivers/fuchsia_driver_package.gni")
import("//build/rust/rustc_binary.gni")

group("driver-index") {
  deps = [ ":package" ]
}

group("bootfs") {
  deps = [ ":component" ]
}

group("tests") {
  testonly = true
  deps = [ ":driver-index-unittests" ]
}

rustc_binary("bin") {
  output_name = "driver_index"
  force_opt = "z"

  # Driver Index must always be small (even in debug builds) since it
  # is part of the Zircon boot image.
  configs += [ "//build/config/lto:thinlto" ]
  configs -= [ "//build/config/lto:thinlto" ]
  configs += [ "//build/config/lto:thinlto" ]
  configs -= [ "//build/config/fuchsia:dynamic_rust_standard_library" ]

  # Generates a GN target for unit-tests with the label `bin_test`, and
  # a binary named `driver_index_bin_test`.
  with_unit_tests = true

  deps = [
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.data:fuchsia.data-rustc",
    "//sdk/fidl/fuchsia.driver.framework:fuchsia.driver.framework-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/devices/lib/bind:lib",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-url",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/cm_rust",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:url",
  ]

  sources = [
    "src/main.rs",
    "src/package_resolver.rs",
  ]
}

fuchsia_component("component") {
  component_name = "driver-index"
  manifest = "meta/driver-index.cml"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  package_name = "driver-index"
  deps = [ ":component" ]
}

driver_bind_rules("test-bind") {
  rules = "test.bind"
}

fuchsia_driver_component("test-bind-component") {
  bind_path = "meta/bind/test-bind.bindbc"
  driver_path = "driver/not-real.so"
  deps = [ ":test-bind" ]
}

driver_bind_rules("test-bind2") {
  rules = "test2.bind"
}

fuchsia_driver_component("test-bind2-component") {
  bind_path = "meta/bind/test-bind2.bindbc"
  driver_path = "driver/not-real2.so"
  deps = [ ":test-bind2" ]
}

driver_manifest("driver-manifest") {
  manifest_name = "base-driver-manifest.json"
  install_as_config_resource = true
  package_url = "fuchsia-pkg://fuchsia.com/driver-index-unittests"
  deps = [
    ":test-bind-component",
    ":test-bind2-component",
  ]
}

# Run with `fx test driver-index-unittests`.
fuchsia_unittest_package("driver-index-unittests") {
  manifest = "meta/driver-index-unittests.cml"
  deps = [
    ":bin_test",
    ":driver-manifest",
    ":test-bind-component",
    ":test-bind2-component",
  ]
}
