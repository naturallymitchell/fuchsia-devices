# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":chromiumos-ec-core-tests",
    ":chromiumos-ec-motion-tests",
    ":chromiumos-ec-usb-pd-tests",
  ]
}

driver_bind_rules("bind") {
  rules = "chromiumos_ec_core.bind"
  header_output = "chromiumos_ec_core_bind.h"
  bind_output = "chromiumos_ec_core.bindbc"
  deps = [ "//src/devices/bind/fuchsia.hardware.google.ec" ]
}

source_set("core") {
  sources = [
    "chromiumos_ec_core.cc",
    "chromiumos_ec_core.h",
    "subdriver.cc",
    "subdriver.h",
  ]
  public_deps = [
    ":bind",
    "//sdk/fidl/fuchsia.hardware.google.ec:fuchsia.hardware.google.ec_llcpp",
    "//sdk/lib/fit-promise",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/svc",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/chromiumos-platform-ec",
  ]
}

source_set("test-common") {
  sources = [
    "fake_device.cc",
    "fake_device.h",
  ]

  public_deps = [
    ":core",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]

  testonly = true
}

source_set("motion") {
  sources = [
    "motion.cc",
    "motion.h",
    "subdriver.h",
  ]

  public_deps = [
    ":core",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//zircon/system/ulib/hid",
  ]
}

source_set("usb-pd") {
  sources = [
    "subdriver.h",
    "usb_pd.cc",
    "usb_pd.h",
  ]

  public_deps = [
    ":core",
    "//sdk/fidl/fuchsia.hardware.power:fuchsia.hardware.power_llcpp",
  ]
}

fuchsia_driver("driver") {
  output_name = "chromiumos-ec-core"
  deps = [
    ":core",
    ":motion",
    ":usb-pd",
  ]
}

fuchsia_driver_component("component") {
  info = "component-info.json"
  component_name = "chromiumos-ec-core"
  deps = [ ":driver" ]
}

fuchsia_system_package("chromiumos-ec-core") {
  deps = [ ":component" ]

  # Remove the line below when this package is only used by the system image.
  allowed_in_extra_deps = true
}

test("chromiumos-ec-core-unit-tests") {
  sources = [ "chromiumos_ec_core_tests.cc" ]
  deps = [ ":test-common" ]
}

test("chromiumos-ec-motion-unit-tests") {
  sources = [ "motion-test.cc" ]
  deps = [
    ":motion",
    ":test-common",
    "//zircon/system/ulib/hid-parser",
  ]
}

test("chromiumos-ec-usb-pd-unit-tests") {
  sources = [ "usb_pd_test.cc" ]
  deps = [
    ":test-common",
    ":usb-pd",
  ]
}

fuchsia_unittest_package("chromiumos-ec-core-tests") {
  deps = [ ":chromiumos-ec-core-unit-tests" ]
}

fuchsia_unittest_package("chromiumos-ec-motion-tests") {
  deps = [ ":chromiumos-ec-motion-unit-tests" ]
}

fuchsia_unittest_package("chromiumos-ec-usb-pd-tests") {
  deps = [ ":chromiumos-ec-usb-pd-unit-tests" ]
}
