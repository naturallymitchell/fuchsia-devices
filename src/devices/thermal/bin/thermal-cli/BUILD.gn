# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

executable("thermal-cli") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "main.cc" ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
  ]
}

source_set("common") {
  sources = [
    "thermal-cli.cc",
    "thermal-cli.h",
  ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/zx",
  ]
}

test("thermal-cli-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "thermal-cli-test"
  sources = [ "test/thermal-cli-test.cc" ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_c",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fidl-utils",
  ]
}

migrated_manifest("thermal-cli-manifest") {
  deps = [ ":thermal-cli" ]
}

unittest_package("thermal-cli-test-package") {
  package_name = "thermal-cli-test"
  deps = [ ":thermal-cli-test" ]

  tests = [
    {
      name = "thermal-cli-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":thermal-cli-test-package" ]
}
