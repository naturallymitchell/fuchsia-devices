# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("aml-pwm") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "aml-pwm.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

test("aml-pwm-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "aml-pwm-test"
  sources = [
    "aml-pwm-test.cc",
    "aml-pwm.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake_ddk",
    "//zircon/system/dev/lib/mock-mmio-reg",
  ]
}

group("common") {
  public_deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/hwreg",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/banjo/ddk.protocol.pwm",
    "//zircon/system/dev/lib/amlogic",
    "//zircon/system/dev/lib/device-protocol-pdev",
  ]
}

migrated_manifest("aml-pwm-manifest") {
  deps = [ ":aml-pwm" ]
}

unittest_package("aml-pwm-test-package") {
  package_name = "aml-pwm-test"
  deps = [ ":aml-pwm-test" ]

  tests = [
    {
      name = "aml-pwm-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":aml-pwm-test-package" ]
}
