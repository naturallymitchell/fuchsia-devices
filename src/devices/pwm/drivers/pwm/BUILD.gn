# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("pwm") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:static_cpp_standard_library",
    "//build/unification/config:zircon-migrated",
  ]
  sources = [ "pwm.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

test("pwm-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "pwm-test"
  sources = [
    "pwm-test.cc",
    "pwm.cc",
  ]
  deps = [
    ":common",
    "//src/devices/testing/fake_ddk",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

group("common") {
  public_deps = [
    "//sdk/banjo/ddk.protocol.pwm",
    "//src/devices/lib/amlogic",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
  ]
}

unittest_package("pwm-test-package") {
  package_name = "pwm-test"
  deps = [ ":pwm-test" ]

  tests = [
    {
      name = "pwm-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":pwm-test-package" ]
}
