# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

driver_bind_rules("pwm-bind") {
  rules = "pwm.bind"
  header_output = "pwm-bind.h"
  bind_output = "pwm.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.pwm" ]
}

driver_module("pwm-driver") {
  output_name = "pwm"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "pwm.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("pwm") {
  component_name = "pwm"
  deps = [ ":pwm-driver" ]
}

test("pwm-test-bin") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "pwm-test"
  sources = [
    "pwm-test.cc",
    "pwm.cc",
  ]
  deps = [
    ":common",
    "//src/devices/testing/no_ddk",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

group("common") {
  public_deps = [
    ":pwm-bind",
    "//sdk/banjo/fuchsia.hardware.pwm:fuchsia.hardware.pwm_banjo_cpp",
    "//src/devices/lib/amlogic",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_unittest_package("pwm-test") {
  deps = [ ":pwm-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":pwm-bind_test",
    ":pwm-test",
  ]
}
