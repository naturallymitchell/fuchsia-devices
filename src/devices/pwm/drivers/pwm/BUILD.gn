# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("pwm") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "pwm.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

test("pwm-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "pwm-test"
  sources = [
    "pwm-test.cc",
    "pwm.cc",
  ]
  deps = [
    ":common",
    "//zircon/public/lib/fake_ddk",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

group("common") {
  public_deps = [
    "//zircon/public/lib/amlogic",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/banjo/ddk.protocol.pwm",
  ]
}

migrated_manifest("pwm-manifest") {
  deps = [ ":pwm" ]
}

migrated_manifest("pwm-test-manifest") {
  deps = [ ":pwm-test" ]
}
