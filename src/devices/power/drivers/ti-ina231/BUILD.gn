# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/drivers/fuchsia_driver_component.gni")
import("//build/test.gni")

source_set("metadata") {
  public = [ "ti-ina231-metadata.h" ]
}

group("common") {
  public_deps = [
    ":bind",
    "//sdk/banjo/fuchsia.hardware.i2c:fuchsia.hardware.i2c_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zxc",
  ]
}

driver_bind_rules("bind") {
  rules = "ti-ina231.bind"
  header_output = "ti-ina231-bind.h"
  bind_output = "ti-ina231.bindbc"
  tests = "ti-ina231-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.ti.platform" ]
}

driver_module("ti-ina231-driver") {
  output_name = "ti-ina231"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "ti-ina231.cc" ]
  deps = [ ":common" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("ti-ina231") {
  component_name = "ti-ina231"
  deps = [ ":ti-ina231-driver" ]
}

test("ti-ina231-test-bin") {
  output_name = "ti-ina231-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "ti-ina231-test.cc",
    "ti-ina231.cc",
  ]
  deps = [
    ":common",
    "//src/devices/i2c/testing/fake-i2c",
    "//src/devices/testing/fake_ddk",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/69585): This target uses raw zx::channel with LLCPP which is deprecated.
  # Please migrate to typed channel APIs (fidl::ClientEnd<T>, fidl::ServerEnd<T>).
  # See linked bug for details.
  configs += [ "//build/cpp:fidl-llcpp-deprecated-raw-channels" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("ti-ina231-test") {
  deps = [ ":ti-ina231-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":bind_test",
    ":ti-ina231-test",
  ]
}
